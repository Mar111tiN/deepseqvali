def IR_ref(_):
    '''
    returns ref parameters for realign-refs gatk tools
    '''

    return f"-R {full_path('genome')} -known {full_path('gold_standard_indels')} -known {full_path('phase1k_indels')}"


rule create_GATK_target_list:
    input:
        unpack(get_IR_input)
    output:
        "realigned/{sample}_{type}.{chrom}.intervals"
    threads:
        config['realignGATK']['threads']
    log:
        "logs/realign/{sample}_{type}.{chrom}.targetList"
    conda:
        f"../{config['envs']}/gatk3-env.yml"
    params:
        gatk = config['tools']['gatk3'],
        ref = IR_ref
    shell:
        "{params.gatk} RealignerTargetCreator -nt {threads} {params.ref} -I {input.bam} -o {output} &>{log}"


rule realignGATK:
    input:
        unpack(get_IR_input),
        intervals = "realigned/{sample}_{type}.{chrom}.intervals"
    output:
        bam= "realigned/{sample}_{type}.{chrom}.bam"
    log:
        "logs/realign/{sample}_{type}.{chrom}.log"
    threads:
        config['realignGATK']['threads']
    conda:
        f"../{config['envs']}/gatk3-env.yml"
    params:
        gatk = config['tools']['gatk3'],
        ref = IR_ref,
        deflater = "-jdk_deflater -jdk_inflater" if config['envs'] == "envMac" else ""
    shell:
        "{params.gatk} IndelRealigner {params.deflater} {params.ref} "
        "-targetIntervals {input.intervals} -I {input.bam} -o {output.bam} &>{log}; "


rule base_recalib:
    input:
        bam = "realigned/{sample}_{type}.{chrom}.bam"
    output:
        table = "recaltable/{sample}_{type}.{chrom}.recal1"
    log:
        "logs/recalib/{sample}_{type}.{chrom}.log"
    threads:
        config['recalib']['threads']
    conda:
        f"../{config['envs']}/gatk4-env.yml"
    params:
        gatk = config['tools']['gatk'],
        genome = f"-R {full_path('genome')}",
        known_sites = f"--known-sites {full_path('gold_standard_indels')} --known-sites {full_path('phase1k_indels')} --known-sites {full_path(config['recalib']['known_sites'])}"
    shell:
        "{params.gatk} BaseRecalibrator -I {input.bam} {params.genome} {params.known_sites} -O {output.table} &>{log}; "


# here I need a switch to just split 
rule apply_BQSR:
    input:
        bam = "realigned/{sample}_{type}.{chrom}.bam",
        table = "recaltable/{sample}_{type}.{chrom}.recal1"
    output:
        bam = "recalib/{sample}_{type}.{chrom}.bam",
        table = "recaltable/{sample}_{type}.{chrom}.recal2"
    log:
        "logs/apply_BQSR/{sample}_{type}.{chrom}.log"
    threads:
        config['recalib']['threads']
    conda:
        f"../{config['envs']}/gatk4-env.yml"
    params:
        gatk = config['tools']['gatk'],
        genome = f"-R {full_path('genome')}",
        known_sites = f"--known-sites {full_path('gold_standard_indels')} --known-sites {full_path('phase1k_indels')} --known-sites {full_path(config['recalib']['known_sites'])}"  
    shell:
        # apply recalibration table on input
        "{params.gatk} ApplyBQSR -I {input.bam} {params.genome} --bqsr-recal-file {input.table} -O {output.bam}; "  #  &>>{log}
        # recreate recalibration table from output for QC
        "{params.gatk} BaseRecalibrator -I {output.bam} {params.genome} {params.known_sites} -O {output.table}"  #  &>>{log}
